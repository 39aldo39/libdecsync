buildscript {
    ext.kotlin_version = '1.4.20'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'

group = "org.decsync"
version = "1.8.1"

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 19
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:rules:1.3.0"
}

kotlin {
    android {
        publishLibraryVariantsGroupedByFlavor = true
        publishAllLibraryVariants()
    }
    linuxX64 {
        binaries {
            sharedLib {
                baseName = "decsync"
            }
        }
    }
    mingwX64 {
        binaries {
            sharedLib {
                baseName = "decsync"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        nativeMain {
            dependsOn commonMain
        }
        nativeTest {
            dependsOn commonTest
        }
        linuxX64Main {
            dependsOn nativeMain
        }
        linuxX64Test {
            dependsOn nativeTest
        }
        mingwX64Main {
            dependsOn nativeMain
        }
        mingwX64Test {
            dependsOn nativeTest
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://api.bintray.com/maven/39aldo39/maven/libdecsync"
            credentials {
                username = project.hasProperty("BINTRAY_USER") ? BINTRAY_USER : System.getenv("BINTRAY_USER")
                password = project.hasProperty("BINTRAY_KEY") ? BINTRAY_KEY : System.getenv("BINTRAY_KEY")
            }
        }
    }
}
