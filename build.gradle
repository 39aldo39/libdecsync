buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'

group = "org.decsync"
version = "1.1.3"

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 5
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    android {
        publishLibraryVariantsGroupedByFlavor = true
        publishAllLibraryVariants()
    }
    linuxX64 {
        binaries {
            sharedLib {
                baseName = "decsync"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.13.0"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.13.0"
            }
        }
        linuxX64Main {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.13.0"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://api.bintray.com/maven/39aldo39/maven/libdecsync"
            credentials {
                username = project.hasProperty("bintrayUser") ? bintrayUser : ""
                password = project.hasProperty("bintrayApiKey") ? bintrayApiKey : ""
            }
        }
    }
}